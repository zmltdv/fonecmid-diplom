
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда  
	
	#Область СлужебныеПроцедурыИФункции 
	
	Функция СоздатьСписокНаСервере(Знач ДатаНачала, Знач ДатаОкончания) Экспорт
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Договор,
		|	МАКСИМУМ(РеализацияТоваровУслуг.Ссылка) КАК Реализация
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ПО (РеализацияТоваровУслуг.Договор = ДоговорыКонтрагентов.Ссылка)
		|			И (РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания)
		|ГДЕ
		|	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание)
		|	И ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействияДоговора <= &ДатаНачала
		|	И ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействияДоговора >= &ДатаОкончания
		|
		|СГРУППИРОВАТЬ ПО
		|	ДоговорыКонтрагентов.Ссылка";
		
		Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
		
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		ВсегоДоговоров = РезультатЗапроса.Выбрать().Количество();
		Счетчик = 0;
		
		СписокРеализацийМассив = Новый Массив;
		
		Пока Выборка.Следующий() Цикл
			Счетчик = Счетчик + 1;
			ДлительныеОперации.СообщитьПрогресс(Счетчик * 100 / ВсегоДоговоров, "Обработка договоров...");
			
			СтруктураРеализации = Новый Структура("Договор, Реализация");
			СтруктураРеализации.Договор = Выборка.Договор;
			
			// Если реализация за период уже существует
			Если ЗначениеЗаполнено(Выборка.Реализация) Тогда
				ДокументОбъект = Выборка.Реализация.ПолучитьОбъект();
                ДокументОбъект.ВКМ_ВыполнитьАвтозаполнение(Выборка.Договор);
				
				Если ДокументОбъект.ПроверитьЗаполнение() Тогда
					ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
					СтруктураРеализации.Реализация = Выборка.Реализация;
				Иначе
					// Записываем как есть при ошибке заполнения
					ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
					СтруктураРеализации.Реализация = Выборка.Реализация;
					ЗаписьЖурналаРегистрации("Предупреждение", 
					"Документ не обновлен: " + Выборка.Реализация + ". Не все обязательные данные заполнены",
					УровеньЖурналаРегистрации.Предупреждение);
				КонецЕсли;
				
			Иначе
				// Создаем новую реализацию
				СтруктураРеализации.Реализация = СоздатьНовуюРеализацию(Выборка.Договор, ДатаОкончания);
			КонецЕсли;
			
			СписокРеализацийМассив.Добавить(СтруктураРеализации);
		КонецЦикла; 
		
		Возврат СписокРеализацийМассив; 
		
	КонецФункции
	
	Функция СоздатьНовуюРеализацию(Договор, ДатаСозданияНовойРеализации)
		
		Попытка
			НоваяРеализация = Документы.РеализацияТоваровУслуг.СоздатьДокумент(); 
			НоваяРеализация.Дата = ДатаСозданияНовойРеализации; 
			НоваяРеализация.Ответственный = Пользователи.ТекущийПользователь(); 
			НоваяРеализация.Договор = Договор; 
			НоваяРеализация.Контрагент = Договор.Владелец; 
			НоваяРеализация.Организация = Договор.Организация;    
			
			// Вызываем метод автозаполнения
			НоваяРеализация.ВКМ_ВыполнитьАвтозаполнение(Договор); 
			
			// Проверяем заполнение перед проведением
			Если НоваяРеализация.ПроверитьЗаполнение() Тогда
				НоваяРеализация.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный); 
				Возврат НоваяРеализация.Ссылка;
			Иначе
				// Если не прошла проверка, записываем без проведения
				НоваяРеализация.Записать(РежимЗаписиДокумента.Запись);
				ЗаписьЖурналаРегистрации("Предупреждение", 
				"Документ не проведен: " + НоваяРеализация.Ссылка + ". Не все обязательные данные заполнены",
				УровеньЖурналаРегистрации.Предупреждение);
				Возврат НоваяРеализация.Ссылка;
			КонецЕсли;
		Исключение
			// В случае ошибки возвращаем Неопределено
			ЗаписьЖурналаРегистрации("Ошибка", 
			"Ошибка создания реализации: " + ОписаниеОшибки(),
			УровеньЖурналаРегистрации.Ошибка);
			Возврат Неопределено;
		КонецПопытки;
		
	КонецФункции
	
	#КонецОбласти
	
#КонецЕсли
